// SPDX-License-Identifier: MIT
pragma solidity ^0.8.30;

import {FailLogics} from "./FailLogics.sol";
import {BLS} from "./libraries/BLS.sol";

contract CommitReveal2BLSOptimized is FailLogics {
    address public governanceMultisig;
    BLS.G1Point public s_aggregatedBLSPubKey;
    mapping(address => BLS.G1Point) public s_operatorBLSPubKeys;

    error UnauthorizedGovernance();
    error BLSVerificationFailed();

    modifier onlyGovernance() {
        if (msg.sender != governanceMultisig) revert UnauthorizedGovernance();
        _;
    }

    modifier onlyWhenCompleted() {
        assembly ("memory-safe") {
            if iszero(eq(sload(s_isInProcess.slot), COMPLETED)) {
                mstore(0, 0x644a8033) // selector for NotCompletedStatus()
                revert(0x1c, 0x04)
            }
        }
        _;
    }

    constructor(
        uint256 activationThreshold,
        uint256 flatFee,
        string memory name,
        string memory version,
        uint256 offChainSubmissionPeriod,
        uint256 requestOrSubmitOrFailDecisionPeriod,
        uint256 onChainSubmissionPeriod,
        uint256 offChainSubmissionPeriodPerOperator,
        uint256 onChainSubmissionPeriodPerOperator,
        uint256 maxGasPrice,
        address _governanceMultisig
    ) payable FailLogics(name, version) {
        require(msg.value >= activationThreshold);
        s_depositAmount[msg.sender] = msg.value;
        s_activationThreshold = activationThreshold;
        s_flatFee = flatFee;
        s_offChainSubmissionPeriod = offChainSubmissionPeriod;
        s_requestOrSubmitOrFailDecisionPeriod = requestOrSubmitOrFailDecisionPeriod;
        s_onChainSubmissionPeriod = onChainSubmissionPeriod;
        s_offChainSubmissionPeriodPerOperator = offChainSubmissionPeriodPerOperator;
        s_onChainSubmissionPeriodPerOperator = onChainSubmissionPeriodPerOperator;
        s_isInProcess = COMPLETED;
        s_maxGasPrice = maxGasPrice;
        governanceMultisig = _governanceMultisig;
    }

    function setEconomicParameters(uint256 activationThreshold, uint256 flatFee)
        external
        onlyGovernance
        onlyWhenCompleted
    {
        assembly ("memory-safe") {
            let m := mload(0x40)
            sstore(s_activationThreshold.slot, activationThreshold)
            sstore(s_flatFee.slot, flatFee)
            mstore(m, activationThreshold)
            mstore(add(m, 0x20), flatFee)
            log1(m, 0x40, 0x08f0774e7eb69e2d6a7cf2192cbf9c6f519a40bcfa16ff60d3f18496585e46dc) // EconomicParametersSet
            mstore(0x40, m) // Restore the free memory pointer
        }
    }

    function setPeriods(
        uint256 offChainSubmissionPeriod,
        uint256 requestOrSubmitOrFailDecisionPeriod,
        uint256 onChainSubmissionPeriod,
        uint256 offChainSubmissionPeriodPerOperator,
        uint256 onChainSubmissionPeriodPerOperator
    ) external onlyGovernance notInProgress {
        assembly ("memory-safe") {
            let m := mload(0x40)
            sstore(s_offChainSubmissionPeriod.slot, offChainSubmissionPeriod)
            sstore(s_requestOrSubmitOrFailDecisionPeriod.slot, requestOrSubmitOrFailDecisionPeriod)
            sstore(s_onChainSubmissionPeriod.slot, onChainSubmissionPeriod)
            sstore(s_offChainSubmissionPeriodPerOperator.slot, offChainSubmissionPeriodPerOperator)
            sstore(s_onChainSubmissionPeriodPerOperator.slot, onChainSubmissionPeriodPerOperator)
            mstore(m, offChainSubmissionPeriod)
            mstore(add(m, 0x20), requestOrSubmitOrFailDecisionPeriod)
            mstore(add(m, 0x40), onChainSubmissionPeriod)
            mstore(add(m, 0x60), offChainSubmissionPeriodPerOperator)
            mstore(add(m, 0x80), onChainSubmissionPeriodPerOperator)
            log1(m, 0xa0, 0xe0fd8eabd2cc23ea87b43a00ac588c61789ad28d3edfeb76613f623fa1f6bd08) // event PeriodsSet(uint256 offChainSubmissionPeriod, uint256 requestOrSubmitOrFailDecisionPeriod, uint256 onChainSubmissionPeriod, uint256 offChainSubmissionPeriodPerOperator, uint256 onChainSubmissionPeriodPerOperator)
            mstore(0x40, m) // Restore the free memory pointer
        }
    }

    function setGasParameters(
        uint128 gasUsedMerkleRootSubAndGenRandNumA,
        uint128 gasUsedMerkleRootSubAndGenRandNumBWithLeaderOverhead,
        uint256 maxCallbackGasLimit,
        uint48 getL1UpperBoundGasUsedWhenCalldataSize4,
        uint48 failToRequestCvOrSubmitMerkleRootGasUsed,
        uint48 failToSubmitMerkleRootAfterDisputeGasUsed,
        uint48 failToRequestSOrGenerateRandomNumberGasUsed,
        uint48 failToSubmitSGasUsed,
        uint32 failToSubmitCoGasUsedBaseA,
        uint32 failToSubmitCvGasUsedBaseA,
        uint32 failToSubmitGasUsedBaseB,
        uint32 perOperatorIncreaseGasUsedA,
        uint32 perOperatorIncreaseGasUsedB,
        uint32 perAdditionalDidntSubmitGasUsedA,
        uint32 perAdditionalDidntSubmitGasUsedB,
        uint32 perRequestedIncreaseGasUsed,
        uint256 maxGasPrice
    ) external onlyGovernance onlyWhenCompleted {
        assembly ("memory-safe") {
            sstore(
                s_gasUsedMerkleRootSubAndGenRandNumA.slot,
                or(gasUsedMerkleRootSubAndGenRandNumA, shl(128, gasUsedMerkleRootSubAndGenRandNumBWithLeaderOverhead))
            )
            sstore(s_maxCallbackGasLimit.slot, maxCallbackGasLimit)

            sstore(
                s_getL1UpperBoundGasUsedWhenCalldataSize4.slot,
                or(
                    getL1UpperBoundGasUsedWhenCalldataSize4,
                    or(
                        shl(FAILTOREQUESTSUBMITCV_OR_SUBMITMEKRLEROOT_OFFSET, failToRequestCvOrSubmitMerkleRootGasUsed),
                        or(
                            shl(FAILTOSUBMITMERKLEROOTAFTERDISPUTE_OFFSET, failToSubmitMerkleRootAfterDisputeGasUsed),
                            or(
                                shl(
                                    FAILTOREQUESTS_OR_GENERATERANDOMNUMBER_OFFSET,
                                    failToRequestSOrGenerateRandomNumberGasUsed
                                ),
                                shl(FAILTOSUBMITS_OFFSET, failToSubmitSGasUsed)
                            )
                        )
                    )
                )
            )
            sstore(
                s_failToSubmitCoGasUsedBaseA.slot,
                or(
                    failToSubmitCoGasUsedBaseA,
                    or(
                        shl(FAILTOSUBMITCVGASUSEDBASEA_OFFSET, failToSubmitCvGasUsedBaseA),
                        or(
                            shl(FAILTOSUBMITGASUSEDBASEB_OFFSET, failToSubmitGasUsedBaseB),
                            or(
                                shl(PEROPERATORINCREASEGASUSEDA_OFFSET, perOperatorIncreaseGasUsedA),
                                or(
                                    shl(PEROPERATORINCREASEGASUSEDB_OFFSET, perOperatorIncreaseGasUsedB),
                                    or(
                                        shl(PERADDITIONALDIDNTSUBMITGASUSEDA_OFFSET, perAdditionalDidntSubmitGasUsedA),
                                        or(
                                            shl(
                                                PERADDITIONALDIDNTSUBMITGASUSEDB_OFFSET,
                                                perAdditionalDidntSubmitGasUsedB
                                            ),
                                            shl(PERREQUESTEDINCREASEGASUSED_OFFSET, perRequestedIncreaseGasUsed)
                                        )
                                    )
                                )
                            )
                        )
                    )
                )
            )
            sstore(s_maxGasPrice.slot, maxGasPrice)
        }

        emit GasParametersSet(
            gasUsedMerkleRootSubAndGenRandNumA,
            gasUsedMerkleRootSubAndGenRandNumBWithLeaderOverhead,
            maxCallbackGasLimit,
            getL1UpperBoundGasUsedWhenCalldataSize4,
            failToRequestCvOrSubmitMerkleRootGasUsed,
            failToSubmitMerkleRootAfterDisputeGasUsed,
            failToRequestSOrGenerateRandomNumberGasUsed,
            failToSubmitSGasUsed,
            failToSubmitCoGasUsedBaseA,
            failToSubmitCvGasUsedBaseA,
            failToSubmitGasUsedBaseB,
            perOperatorIncreaseGasUsedA,
            perOperatorIncreaseGasUsedB,
            perAdditionalDidntSubmitGasUsedA,
            perAdditionalDidntSubmitGasUsedB,
            perRequestedIncreaseGasUsed,
            maxGasPrice
        );
    }

    function estimateRequestPrice(uint32 callbackGasLimit, uint256 gasPrice) external view returns (uint256) {
        uint256 activatedOperatorsLength = s_activatedOperators.length;
        require(activatedOperatorsLength > 1, NotEnoughActivatedOperators());
        return _calculateRequestPrice(callbackGasLimit, gasPrice, activatedOperatorsLength);
    }

    function estimateRequestPriceWithNumOfOperators(uint32 callbackGasLimit, uint256 gasPrice, uint256 numOfOperators)
        external
        view
        returns (uint256)
    {
        return _calculateRequestPrice(callbackGasLimit, gasPrice, numOfOperators);
    }

    function requestRandomNumber(uint32 callbackGasLimit) external payable virtual returns (uint256) {
        uint256 activatedOperatorsLength = s_activatedOperators.length;
        // ** check if the fee amount is enough
        require(
            msg.value >= _calculateRequestPrice(callbackGasLimit, tx.gasprice, activatedOperatorsLength),
            InsufficientAmount()
        );
        assembly ("memory-safe") {
            let m := mload(0x40)
            // ** check if the contract is halted (moved upfront for gas optimization)
            let currentState := sload(s_isInProcess.slot)
            if eq(currentState, HALTED) {
                mstore(0, 0x2caa910c) // selector for CannotRequestWhenHalted()
                revert(0x1c, 0x04)
            }
            // ** check if the callbackGasLimit is within the limit
            if gt(callbackGasLimit, sload(s_maxCallbackGasLimit.slot)) {
                mstore(0, 0x1cf7ab79) // selector for ExceedCallbackGasLimit()
                revert(0x1c, 0x04)
            }
            // ** check if there are enough activated operators
            if lt(activatedOperatorsLength, 2) {
                mstore(0, 0x77599fd9) // selector for NotEnoughActivatedOperators()
                revert(0x1c, 0x04)
            }
            // ** check if the leader has enough deposit
            mstore(0x00, sload(_OWNER_SLOT))
            mstore(0x20, s_depositAmount.slot)
            if lt(sload(keccak256(0x00, 0x40)), sload(s_activationThreshold.slot)) {
                mstore(0, 0xc0013a5a) // selector for LeaderLowDeposit()
                revert(0x1c, 0x04)
            }
            let newRound := sload(s_requestCount.slot)
            sstore(s_requestCount.slot, add(newRound, 1)) // update the request count
            if gt(sub(newRound, sload(s_currentRound.slot)), 2000) {
                mstore(0, 0x02cd147b) // selector for TooManyRequestsQueued()
                revert(0x1c, 0x04)
            }

            // ** set the round bit
            // calculate the storage slot corresponding to the round
            // wordPos = round >> 8
            mstore(0, shr(8, newRound))
            mstore(0x20, s_roundBitmap.slot)
            // the slot of self[wordPos] is keccak256(abi.encode(wordPos, self.slot))
            let slot := keccak256(0, 0x40)
            // mask = 1 << bitPos = 1 << (round & 0xff)
            // self[wordPos] |= mask
            sstore(slot, or(sload(slot), shl(and(newRound, 0xff), 1)))
            let startTime
            // ** check if the current round is completed
            // ** if the current round is completed, start a new round
            if eq(currentState, COMPLETED) {
                startTime := timestamp()
                sstore(s_currentRound.slot, newRound)
                sstore(s_isInProcess.slot, IN_PROGRESS)
                mstore(0, newRound)
                mstore(0x20, 0) // trialNum is 0 for the first trial
                mstore(0x40, IN_PROGRESS)
                log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
            }
            // *** store the request info
            mstore(0x00, newRound)
            mstore(0x20, s_requestInfo.slot)
            let requestInfoSlot := keccak256(0x00, 0x40)
            sstore(requestInfoSlot, or(shl(96, caller()), callbackGasLimit))
            sstore(add(requestInfoSlot, 1), startTime)
            sstore(add(requestInfoSlot, 2), callvalue())
            mstore(0x40, m) // Restore the free memory pointer
            return(0x00, 0x20)
        }
    }

    function _calculateRequestPrice(uint32 callbackGasLimit, uint256 gasPrice, uint256 numOfOperators)
        internal
        view
        virtual
        returns (uint256 requestFee)
    {
        assembly ("memory-safe") {
            let gasUsedMerkleRootSubAndGenRandNum := sload(s_gasUsedMerkleRootSubAndGenRandNumA.slot)
            requestFee := add(
                mul(
                    gasPrice,
                    add(
                        callbackGasLimit,
                        add(
                            mul(
                                and(gasUsedMerkleRootSubAndGenRandNum, GASUSED_MERKLEROOTSUB_GENRANDNUM_MASK),
                                numOfOperators
                            ),
                            shr(128, gasUsedMerkleRootSubAndGenRandNum) // gasUsedMerkleRootSubAndGenRandNumBWithLeaderOverhead
                        )
                    )
                ),
                sload(s_flatFee.slot)
            )
        }
    }

    function submitMerkleRoot(bytes32 merkleRoot, BLS.G2Point calldata blsSignature) external inProgress onlyOwner {
        BLS.G1Point[] memory g1Points = new BLS.G1Point[](2);
        g1Points[0] = NEGATED_G1_GENERATOR();
        g1Points[1] = s_aggregatedBLSPubKey;
        BLS.G2Point[] memory g2Points = new BLS.G2Point[](2);
        g2Points[0] = blsSignature;
        g2Points[1] = BLS.toG2(BLS.Fp2(0, 0, 0, merkleRoot));
        require(BLS.pairing(g1Points, g2Points), BLSVerificationFailed());
        assembly ("memory-safe") {
            let m := mload(0x40)
            // * get trialNum
            let curRound := sload(s_currentRound.slot)
            mstore(0x40, curRound)
            mstore(0x60, s_trialNum.slot)
            mstore(0x20, sload(keccak256(0x40, 0x40))) // trialNum
            // * get merkleRootSubmittedTimestamp
            mstore(0x60, s_merkleRootSubmittedTimestamp.slot)
            mstore(0x40, keccak256(0x40, 0x40))
            let merkleRootSubmittedTimestampSlot := keccak256(0x20, 0x40)
            if gt(sload(merkleRootSubmittedTimestampSlot), 0) {
                mstore(0, 0xa34402b2) // selector for MerkleRootAlreadySubmitted()
                revert(0x1c, 0x04)
            }
            sstore(s_merkleRoot.slot, merkleRoot)
            sstore(merkleRootSubmittedTimestampSlot, timestamp())
            // * emit event MerkleRootSubmitted
            mstore(0x00, curRound)
            // 0x20 already has trialNum
            mstore(0x40, merkleRoot)
            log1(0x00, 0x60, 0x45b19880b523c6750f7f39fca8d77d51101b315495adc482994a4fa2a8294466) // emit event MerkleRootSubmitted(uint256 round, uint256 trialNum, bytes32 merkleRoot)
            mstore(0x40, m) // Restore the free memory pointer
            mstore(0x60, 0) // Restore the zero slot.
        }
    }

    function generateRandomNumber(bytes32[] calldata ss, uint256 packedRevealOrders) external inProgress {
        bytes32 domainSeparator = _domainSeparatorV4();
        assembly ("memory-safe") {
            let m := mload(0x40)
            let activatedOperatorsLength := sload(s_activatedOperators.slot)
            // ** check if all secrets are submitted
            if iszero(eq(activatedOperatorsLength, ss.length)) {
                mstore(0, 0xe0767fa4) // selector for InvalidSecretLength()
                revert(0x1c, 0x04)
            }
            // ** initialize cos and cvs arrays memory, without length data
            let activatedOperatorsLengthInBytes := shl(5, activatedOperatorsLength)
            let cos := m
            let cvs := add(add(cos, activatedOperatorsLengthInBytes), 1) // add 1 for the index
            let secrets := add(cvs, activatedOperatorsLengthInBytes)
            mstore(0x40, add(secrets, activatedOperatorsLengthInBytes)) // update the free memory pointer

            // ** get cos and cvs
            for { let i } lt(i, activatedOperatorsLengthInBytes) { i := add(i, 0x20) } {
                let secretMemP := add(secrets, i)
                mstore(secretMemP, calldataload(add(ss.offset, i))) // secret
                let cosMemP := add(cos, i)
                mstore(add(cosMemP, 1), shr(5, i))
                mstore(cosMemP, keccak256(secretMemP, 0x20))
                mstore(add(cvs, i), keccak256(cosMemP, 0x21))
            }
            // ** verify reveal order
            let index := and(packedRevealOrders, 0xff) // first reveal index
            let revealBitmap := shl(index, 1)
            mstore(0x00, keccak256(cos, activatedOperatorsLengthInBytes)) // rv
            mstore(0x20, mload(add(cvs, shl(5, index))))
            let before := keccak256(0x00, 0x40)
            // revealOrdersOffset = 0x24
            for { let i := 1 } lt(i, activatedOperatorsLength) { i := add(i, 1) } {
                index := and(calldataload(sub(0x24, i)), 0xff)
                revealBitmap := or(revealBitmap, shl(index, 1))
                mstore(0x20, mload(add(cvs, shl(5, index))))
                let after := keccak256(0x00, 0x40)
                if lt(before, after) {
                    mstore(0, 0x24f1948e) // selector for RevealNotInDescendingOrder()
                    revert(0x1c, 0x04)
                }
                before := after
            }
            if iszero(eq(revealBitmap, sub(shl(activatedOperatorsLength, 1), 1))) {
                mstore(0, 0x06efcba4) // selector for RevealOrderHasDuplicates()
                revert(0x1c, 0x04)
            }
            // ** Create Merkle Root and verify it
            let hashCountInBytes := sub(activatedOperatorsLengthInBytes, 0x20)
            let fmp := mload(0x40) // used to store the hashes
            let cvsPosInBytes
            let hashPosInBytes
            for { let i } lt(i, hashCountInBytes) { i := add(i, 0x20) } {
                switch lt(cvsPosInBytes, activatedOperatorsLengthInBytes)
                case 1 {
                    mstore(0x00, mload(add(cvs, cvsPosInBytes)))
                    cvsPosInBytes := add(cvsPosInBytes, 0x20)
                }
                default {
                    mstore(0x00, mload(add(fmp, hashPosInBytes)))
                    hashPosInBytes := add(hashPosInBytes, 0x20)
                }
                switch lt(cvsPosInBytes, activatedOperatorsLengthInBytes)
                case 1 {
                    mstore(0x20, mload(add(cvs, cvsPosInBytes)))
                    cvsPosInBytes := add(cvsPosInBytes, 0x20)
                }
                default {
                    mstore(0x20, mload(add(fmp, hashPosInBytes)))
                    hashPosInBytes := add(hashPosInBytes, 0x20)
                }
                mstore(add(fmp, i), keccak256(0x00, 0x40))
            }
            // ** check if the merkle root is submitted
            let round := sload(s_currentRound.slot)
            mstore(0x20, round)
            mstore(0x40, s_trialNum.slot)
            let trialNum := sload(keccak256(0x20, 0x40))
            mstore(0x00, trialNum)
            mstore(0x40, s_merkleRootSubmittedTimestamp.slot)
            mstore(0x20, keccak256(0x20, 0x40))
            if iszero(sload(keccak256(0x00, 0x40))) {
                mstore(0, 0x8e56b845) // selector for MerkleRootNotSubmitted()
                revert(0x1c, 0x04)
            }
            // ** verify the merkle root
            if iszero(eq(mload(add(fmp, sub(hashCountInBytes, 0x20))), sload(s_merkleRoot.slot))) {
                mstore(0, 0x624dc351) // selector for MerkleVerificationFailed()
                revert(0x1c, 0x04)
            }

            // ** create random number
            let randomNumber := keccak256(secrets, activatedOperatorsLengthInBytes)
            let nextRound := add(round, 1)
            let requestCount := sload(s_requestCount.slot)
            switch eq(nextRound, requestCount)
            case 1 {
                // there is no next round
                sstore(s_isInProcess.slot, COMPLETED)
                mstore(0x00, round)
                mstore(0x20, trialNum)
                mstore(0x40, COMPLETED)
                log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
            }
            default {
                // get next round
                // https://github.com/Uniswap/v4-core/blob/59d3ecf53afa9264a16bba0e38f4c5d2231f80bc/src/libraries/BitMath.sol#L31
                function leastSignificantBit(x) -> r {
                    x := and(x, sub(0, x))
                    r := shl(
                        5,
                        shr(
                            252,
                            shl(
                                shl(
                                    2,
                                    shr(250, mul(x, 0xb6db6db6ddddddddd34d34d349249249210842108c6318c639ce739cffffffff))
                                ),
                                0x8040405543005266443200005020610674053026020000107506200176117077
                            )
                        )
                    )
                    r := or(
                        r,
                        byte(
                            and(div(0xd76453e0, shr(r, x)), 0x1f),
                            0x001f0d1e100c1d070f090b19131c1706010e11080a1a141802121b1503160405
                        )
                    )
                }
                function nextRequestedRound(_round) -> _next, _requested {
                    let wordPos := shr(8, _round)
                    let bitPos := and(_round, 0xff)
                    let mask := not(sub(shl(bitPos, 1), 1))
                    mstore(0x00, wordPos)
                    mstore(0x20, s_roundBitmap.slot)
                    let masked := and(sload(keccak256(0x00, 0x40)), mask)
                    _requested := gt(masked, 0)
                    switch _requested
                    case 1 { _next := sub(add(_round, leastSignificantBit(masked)), bitPos) }
                    default { _next := sub(add(_round, 255), bitPos) }
                }
                let requested
                for { let i } lt(i, 10) { i := add(i, 1) } {
                    nextRound, requested := nextRequestedRound(nextRound)
                    if requested {
                        mstore(0x00, nextRound) // round
                        mstore(0x20, s_requestInfo.slot)
                        sstore(add(keccak256(0x00, 0x40), 1), timestamp()) // startTime
                        sstore(s_currentRound.slot, nextRound)
                        mstore(0x20, 0) // trialNum is 0 for the first trial
                        mstore(0x40, IN_PROGRESS)
                        log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
                        break
                    }
                    if iszero(lt(nextRound, requestCount)) {
                        sstore(s_isInProcess.slot, COMPLETED)
                        let lastRound := sub(requestCount, 1)
                        sstore(s_currentRound.slot, lastRound)
                        mstore(0x00, lastRound)
                        mstore(0x20, 0) // trialNum is 0 for the first trial
                        mstore(0x40, COMPLETED)
                        log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
                        break
                    }
                    nextRound := add(nextRound, 1)
                }
            }
            // ** reward the flatFee to last revealer
            // ** reward the leaderNode (requestFee - flatFee) for submitMerkleRoot and generateRandomNumber
            mstore(0x00, s_activatedOperators.slot)
            mstore(
                0x00,
                sload(
                    add(
                        keccak256(0x00, 0x20), // s_activatedOperators first data slot
                        and(calldataload(sub(0x24, sub(activatedOperatorsLength, 1))), 0xff) // last revealer index, 0x24: revealOrdersOffset
                    )
                )
            ) // last revealer address
            mstore(0x20, s_depositAmount.slot)
            let depositSlot := keccak256(0x00, 0x40) // last revealer
            let flatFee := sload(s_flatFee.slot)
            sstore(depositSlot, add(sload(depositSlot), flatFee))
            // reward sload(add(currentRequestInfoSlot, 2)) - flatFee to the leader
            mstore(0x00, sload(_OWNER_SLOT))
            depositSlot := keccak256(0x00, 0x40) // leader

            mstore(0x00, 0x00fc98b8) // rawFulfillRandomNumber(uint256,uint256) selector
            mstore(0x20, round)
            mstore(0x40, s_requestInfo.slot)
            let currentRequestInfoSlot := keccak256(0x20, 0x40)
            // * update the leader's deposit
            sstore(depositSlot, add(sload(depositSlot), sub(sload(add(currentRequestInfoSlot, 2)), flatFee)))
            mstore(0x40, randomNumber)

            let g := gas()
            // Compute g -= GAS_FOR_CALL_EXACT_CHECK and check for underflow
            // The gas actually passed to the callee is min(gasAmount, 63//64*gas available)
            // We want to ensure that we revert if gasAmount > 63//64*gas available
            // as we do not want to provide them with less, however that check itself costs
            // gas. GAS_FOR_CALL_EXACT_CHECK ensures we have at least enough gas to be able to revert
            // if gasAmount > 63//64*gas available.
            if lt(g, GAS_FOR_CALL_EXACT_CHECK) {
                mstore(0, 0xcea2d914) // NotEnoughGasToRevert()
                revert(0x1c, 0x04)
            }
            g := sub(g, GAS_FOR_CALL_EXACT_CHECK)
            let consumerAndCallbackGasLimitPacked := sload(currentRequestInfoSlot)
            let callbackGasLimit := and(consumerAndCallbackGasLimitPacked, 0xffffffff)
            // if g - g//64 <= gas
            // we subtract g//64 because of EIP-150
            if iszero(gt(sub(g, div(g, 64)), callbackGasLimit)) {
                mstore(0, 0xc5b54909) // NotEnoughGasToCallback()
                revert(0x1c, 0x04)
            }
            // solidity calls check that a contract actually exists at the destination, so we do the same
            let consumer := shr(96, consumerAndCallbackGasLimitPacked)
            if gt(extcodesize(consumer), 0) {
                // call and return whether we succeeded. ignore return data
                // call(gas, addr, value, argsOffset,argsLength,retOffset,retLength)
                pop(call(callbackGasLimit, consumer, 0, 0x1c, 0x44, 0, 0))
            }
            mstore(0x40, secrets) // Restore free memory pointer to secrets (cvs still needed for BLS verification)
            mstore(0x60, 0) // Restore the zero slot.
        }
    }

    function refund(uint256 round) external {
        assembly ("memory-safe") {
            let m := mload(0x40)
            // ** check if the contract is halted
            if iszero(eq(sload(s_isInProcess.slot), HALTED)) {
                mstore(0, 0x78b19eb2) // selector for NotHalted()
                revert(0x1c, 0x04)
            }
            // ** check if the round is valid
            if iszero(lt(round, sload(s_requestCount.slot))) {
                mstore(0, 0x905deff6) // selector for NonExistentRound()
                revert(0x1c, 0x04)
            }
            if lt(round, sload(s_currentRound.slot)) {
                mstore(0, 0x5cafea8c) // selector for RoundAlreadyProcessed()
                revert(0x1c, 0x04)
            }
            mstore(0x00, round)
            mstore(0x20, s_requestInfo.slot)
            let consumerSlot := keccak256(0x00, 0x40)
            // ** check if the caller is the consumer
            if iszero(eq(shr(96, sload(consumerSlot)), caller())) {
                mstore(0, 0x8c7dc13d) // selector for NotConsumer()
                revert(0x1c, 0x04)
            }

            // ** flip the roundBitmap 1 -> 0
            // calculate the storage slot corresponding to the round
            // wordPos = round >> 8
            mstore(0x00, shr(8, round))
            mstore(0x20, s_roundBitmap.slot)
            // the slot of self[wordPos] is keccak256(abi.encode(wordPos, self.slot))
            let slot := keccak256(0, 0x40)
            // mask = 1 << bitPos = 1 << (round & 0xff)
            // self[wordPos] ^= mask
            sstore(slot, xor(sload(slot), shl(and(round, 0xff), 1)))

            // ** refund
            slot := add(consumerSlot, 2) // cost
            let cost := sload(slot)
            if iszero(cost) {
                mstore(0, 0xa85e6f1a) // selector for AlreadyRefunded()
                revert(0x1c, 0x04)
            }
            sstore(slot, 0)
            // Transfer the ETH and check if it succeeded or not.
            if iszero(call(gas(), caller(), cost, 0x00, 0x00, 0x00, 0x00)) {
                mstore(0x00, 0xb12d13eb) // `ETHTransferFailed()`.
                revert(0x1c, 0x04)
            }
            mstore(0x40, m) // Restore the free memory pointer
        }
    }

    function resume() external payable onlyOwner {
        assembly ("memory-safe") {
            let m := mload(0x40)
            if iszero(eq(sload(s_isInProcess.slot), HALTED)) {
                mstore(0, 0x78b19eb2) // selector for NotHalted()
                revert(0x1c, 0x04)
            }
            if lt(sload(s_activatedOperators.slot), 2) {
                mstore(0, 0x77599fd9) // selector for NotEnoughActivatedOperators()
                revert(0x1c, 0x04)
            }
            mstore(0x00, sload(_OWNER_SLOT))
            mstore(0x20, s_depositAmount.slot)
            let ownerDepositSlot := keccak256(0x00, 0x40)
            let ownerDepositAmount := sload(ownerDepositSlot)
            if gt(callvalue(), 0) {
                ownerDepositAmount := add(ownerDepositAmount, callvalue())
                sstore(ownerDepositSlot, ownerDepositAmount)
            }
            if lt(ownerDepositAmount, sload(s_activationThreshold.slot)) {
                mstore(0, 0xc0013a5a) // selector for LeaderLowDeposit()
                revert(0x1c, 0x04)
            }
            let nextRound := sload(s_currentRound.slot)
            let requestCount := sload(s_requestCount.slot)
            let requestCountMinusOne := sub(requestCount, 1)
            let curRound := nextRound
            let requested

            // get next round
            // https://github.com/Uniswap/v4-core/blob/59d3ecf53afa9264a16bba0e38f4c5d2231f80bc/src/libraries/BitMath.sol#L31
            function leastSignificantBit(x) -> r {
                x := and(x, sub(0, x))
                r := shl(
                    5,
                    shr(
                        252,
                        shl(
                            shl(
                                2,
                                shr(250, mul(x, 0xb6db6db6ddddddddd34d34d349249249210842108c6318c639ce739cffffffff))
                            ),
                            0x8040405543005266443200005020610674053026020000107506200176117077
                        )
                    )
                )
                r := or(
                    r,
                    byte(
                        and(div(0xd76453e0, shr(r, x)), 0x1f),
                        0x001f0d1e100c1d070f090b19131c1706010e11080a1a141802121b1503160405
                    )
                )
            }
            function nextRequestedRound(_round) -> _next, _requested {
                let wordPos := shr(8, _round)
                let bitPos := and(_round, 0xff)
                let mask := not(sub(shl(bitPos, 1), 1))
                mstore(0x00, wordPos)
                mstore(0x20, s_roundBitmap.slot)
                let masked := and(sload(keccak256(0x00, 0x40)), mask)
                _requested := gt(masked, 0)
                switch _requested
                case 1 { _next := sub(add(_round, leastSignificantBit(masked)), bitPos) }
                default { _next := sub(add(_round, 255), bitPos) }
            }
            for { let i } lt(i, 10) { i := add(i, 1) } {
                nextRound, requested := nextRequestedRound(nextRound)
                if requested {
                    // Start this requested round
                    mstore(0x00, nextRound)
                    mstore(0x20, s_requestInfo.slot)
                    sstore(add(keccak256(0x00, 0x40), 1), timestamp()) // startTime
                    sstore(s_isInProcess.slot, IN_PROGRESS)
                    mstore(0x40, IN_PROGRESS)
                    switch eq(nextRound, curRound)
                    case 1 {
                        mstore(0x20, s_trialNum.slot)
                        let trialNumSlot := keccak256(0x00, 0x40)
                        let newTrialNum := add(sload(trialNumSlot), 1)
                        sstore(trialNumSlot, newTrialNum)
                        mstore(0x20, newTrialNum)
                        log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
                    }
                    default {
                        sstore(s_currentRound.slot, nextRound)
                        mstore(0x20, 0) // trialNum is 0 for the first trial
                        log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
                    }
                    return(0, 0)
                }
                // If we reach or pass the total request count without finding any requested round,
                // mark as COMPLETED and set the current round to the last possible index.
                // Fixed off-by-one error: now matches generateRandomNumber() boundary logic
                if iszero(lt(nextRound, requestCount)) {
                    sstore(s_isInProcess.slot, COMPLETED)
                    sstore(s_currentRound.slot, requestCountMinusOne)
                    mstore(0x00, requestCountMinusOne)
                    mstore(0x20, 0) // trialNum is 0 for consistency with generateRandomNumber()
                    mstore(0x40, COMPLETED)
                    log1(0x00, 0x60, 0xd42cacab4700e77b08a2d33cc97d95a9cb985cdfca3a206cfa4990da46dd1813) // event Status(uint256 curRound, uint256 curTrialNum, uint256 curState)
                    return(0, 0)
                }
                nextRound := add(nextRound, 1)
            }
            mstore(0x40, m) // Restore the free memory pointer
        }
    }

    function depositAndActivate(BLS.G1Point memory pubKey) external payable notInProgress {
        assembly ("memory-safe") {
            mstore(0x00, caller())
            mstore(0x20, s_depositAmount.slot)
            let depositAmountSlot := keccak256(0x00, 0x40)
            let updatedDepositAmount := add(sload(depositAmountSlot), callvalue())
            if lt(updatedDepositAmount, sload(s_activationThreshold.slot)) {
                mstore(0x00, 0x5af30906) // `LessThanActivationThreshold()`.
                revert(0x1c, 0x04)
            }
            sstore(depositAmountSlot, updatedDepositAmount)
        }
        // Sum pubkey
        s_aggregatedBLSPubKey = BLS.add(s_aggregatedBLSPubKey, pubKey);
        s_operatorBLSPubKeys[msg.sender] = pubKey;
        _activate();
    }

    function _deactivate(uint256 activatedOperatorIndex, address operator) internal override {
        // Subtract operator's pubkey from aggregated pubkey
        s_aggregatedBLSPubKey = BLS.sub(s_aggregatedBLSPubKey, s_operatorBLSPubKeys[operator]);
        delete s_operatorBLSPubKeys[operator];

        assembly ("memory-safe") {
            mstore(0x00, s_activatedOperators.slot)
            let firstActivatedOperatorSlot := keccak256(0x00, 0x20)
            let lastOperatorIndex := sub(sload(s_activatedOperators.slot), 1)
            let lastOperatorAddress := sload(add(firstActivatedOperatorSlot, lastOperatorIndex))
            mstore(0x20, s_activatedOperatorIndex1Based.slot)
            // swap the operator to remove with the last operator in the array
            if iszero(eq(lastOperatorAddress, operator)) {
                sstore(add(firstActivatedOperatorSlot, activatedOperatorIndex), lastOperatorAddress)
                mstore(0x00, lastOperatorAddress)
                sstore(keccak256(0x00, 0x40), add(activatedOperatorIndex, 1))
            }
            // pop the last operator by setting the length to the last index
            sstore(s_activatedOperators.slot, lastOperatorIndex)
            mstore(0x00, operator)
            sstore(keccak256(0x00, 0x40), 0)
            log1(0x00, 0x20, 0x5d10eb48d8c00fb4cc9120533a99e2eac5eb9d0f8ec06216b2e4d5b1ff175a4d) // `DeActivated(address operator)`.
        }
    }

    function NEGATED_G1_GENERATOR() internal pure returns (BLS.G1Point memory) {
        return BLS.G1Point(
            _u(31827880280837800241567138048534752271),
            _u(88385725958748408079899006800036250932223001591707578097800747617502997169851),
            _u(22997279242622214937712647648895181298),
            _u(46816884707101390882112958134453447585552332943769894357249934112654335001290)
        );
    }

    function _u(uint256 x) internal pure returns (bytes32) {
        return bytes32(x);
    }
}
